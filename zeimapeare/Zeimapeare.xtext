grammar zeimapeare.Zeimapeare with org.eclipse.xtext.common.Terminals

generate zeimapeare "http://www.Zeimapeare.zeimapeare"

Program:
	title= TitleOfScript
	
	prologue= Prologue
	
	(acts+=Act)+
;

TitleOfScript:
	name=ID
;

Prologue:
	'PROLOGUE'
	'Wordsmith: Welcome to the theater, lets give an applause to our cast' 
	(actors += ActorDeclaration',')*
	actors += ActorDeclaration'.'

	initials+=InitialAssigment*
	
	main=SceneCall
;

ActorDeclaration: 
	name=ID 'is a member of' datatype = DataType 'family'
;

ActorExtraParameter returns ActorDeclaration:
	'[Enter' name=ID 'from' datatype = DataType 'family]'
;

InitialAssigment returns Assigment:
	actor=[ActorDeclaration] ': I am' expression = Expression
;

Operation:
	operations=('the love' | 'the hostility' | 'the abundance' | 'the dismemberment' | 'the leftovers' | 'M-m-maybe next time...' | 'or' | 'merge or dreams' | 'inmensity')
;


ActorExpression:
	actor= [ActorDeclaration]
;

//Sin el espacio no funciona
ComplexIntExpression:
	(operation= Operation 'of ' exp1=Expression 'and' exp2=Expression)
;

IntExpression:
	ComplexIntExpression | Value 
;

Number:
	number=INT
;

Value:
	Number | ValueSustantive
;

ComplexStringExpression:
	'the embrance of' exp1=Expression 'and' exp2=Expression
;

ValueString:
	value=STRING
;

StringExpression:
	ComplexStringExpression | ValueString
;

Expression:
	IntExpression | StringExpression | ActorExpression
;

RomanNumber:
     romanNumber= ('I' | 'II' | 'III' | 'IV' | 'V' | 'VI' | 'VII' | 'VIII' | 'IX' | 'X')
;

Act:
	'ACT' romanNumber=RomanNumber
	(scene+=Scene)+
;

DataType:
	('Capulet' | 'Montague' | 'Slave')
;

ActorParameter:
	'[Enter' name=[ActorDeclaration]']'
;

Scene:
	'Scene' romanNumber=RomanNumber ':' name = STRING
	actorScene=ActorScene
	(instructions+=Instructions)*
;

ActorScene:
	{ActorScene} (actors+= ActorParameter)*
	(actorsExtra+= ActorExtraParameter)*
;

//Assigment:
//	actor=[ActorDeclaration] ':' AssignmentOP   expression = Expression
//;
//
AssignmentOP:
	subjects=('She is' | 'He is' | 'You are')
;

Assigment:
	actor=[ActorDeclaration] ': ' assignmentOp=AssignmentOP expression = Expression
;

Instructions:
	Assigment | Output | If | SceneCall | ActorSceneCall | Return | While
;

ValueSustantive:
	'a' adj=Adjective? sustantive=Sustantive 
;

Sustantive:
	sustantive= (STRING | 'nothing')
;

Adjective:
	adjectives+=('good')(',' adjectives+=('good'))*
;

Output:
	actor=[ActorDeclaration]': Gossip about your secrets'
;

Return:
	actor=[ActorDeclaration]': I will come back'
;
//TODO: Preguntar a Alexis
// Sin los espacios no sirve :(
Comparison:
	('better' | 'worse') ('or equal')?
;

ParameterSceneCall:
	(actorsExtra+=([ActorDeclaration])(', ' actorsExtra+=[ActorDeclaration])*)
;

SceneCall:
	'Wordsmith: We need to remember the ' sceneCall=[Scene] ('of ' parameterSceneCall=ParameterSceneCall)? '.'
;

ActorSceneCall:
	'( ' actor=[ActorDeclaration] 'remembered that happened in the' sceneCall=[Scene] ('about ' parameterSceneCall=ParameterSceneCall)? ' )'
;

BooleanOperation:
	('otherwise' | 'and')
;

ComplexCondition:
	cond1= Condition ( op=BooleanOperation cond2=ComplexCondition)?
;

Condition:
	exp1=Expression 'is ' comp=Comparison 'than' exp2=Expression
;

If:
	'Wordsmith: If' condition=ComplexCondition
	(instructions+=Instructions)*
	'Wordsmith: Will it continue?'
	else=Else?
;

Else:
	{Else} 'Wordsmith: Unless?'
	(instructions+=Instructions)*
	'Wordsmith: Will it continue?'
;

While:
	'Wordsmith: To be, or not to be, that is the query'
	'Whether' condition= ComplexCondition 
	(instructions+= Instructions)*
	'Wordsmith: The end?'
;

